plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

def mcVersion = project.build_version

version = "$project.mod_version+$mcVersion"
group = project.maven_group

base {
	archivesName = project.mod_id
}

publishMods {
	file = remapJar.archiveFile
	changelog = rootProject.file("CHANGELOG.md").text
	type = STABLE
	displayName = "$mod_name $mod_version ($mcVersion) - $update_name"
	modLoaders.add("fabric")
	version = mod_version

	github {
		accessToken = providers.gradleProperty("secrets.github_token")
		repository = repository_url
		commitish = "master"
		tagName = mod_version
	}

	modrinth {
		accessToken = providers.gradleProperty("secrets.modrinth_token")
		projectId = modrinth_project_id

		for (String version : modrinth_versions.toString().split(' '))
			minecraftVersions.add(version)

		requires("fabric-api")
	}

	discord {
		webhookUrl = providers.gradleProperty("secrets.discord_webhook_url")
		username = "$mod_name Update Tracker"
		content = changelog.map { "# " + displayName + "\n" + it + "\n-# ||<@&1201276927428804741>||" }

		style {
			look = "MODERN"
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

def javaVersion = JavaVersion.VERSION_21
def javaVersionNumber = javaVersion.ordinal() + 1

processResources {
	inputs.property("id", project.mod_id)
	inputs.property("name", project.mod_name)
	inputs.property("version", project.mod_version)
	inputs.property("target", project.target_version)
	inputs.property("loader_target", project.loader_target)
	inputs.property("java_version", javaVersionNumber)

	def map = [
		"id": inputs.properties.id,
		"name": inputs.properties.name,
		"version": inputs.properties.version,
		"target": inputs.properties.target,
		"loader_target": inputs.properties.loader_target,
		"java_version": inputs.properties.java_version
	]

	filesMatching("fabric.mod.json") {
		expand map
	}
}

fabricApi {
	configureDataGeneration() {
		client = true
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = javaVersionNumber
}

loom {
	runs {
		client {
			programArgs(["--username", "axialeaa", "--uuid", "dabdffa7-fdd2-4798-9f94-a518c7856163"])
		}
	}
}

java {
	withSourcesJar()

	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}