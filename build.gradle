plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
}

def mcVersion = stonecutter.current.version

version = property("mod_version") + "+" + mcVersion
group = property("maven_group")

base {
	archivesName = property("archives_base_name")
}

stonecutter {
	swap("schedule_block_tick",
		eval(mcVersion,   "1.17.1") ? "world.getBlockTickScheduler().schedule(pos, this, delay);" : // 1.17.1
		eval(mcVersion, ">=1.19.3") ? "world.scheduleBlockTick(pos, this, delay);" : // 1.19.3+
			"world.createAndScheduleBlockTick(pos, this, delay);" // 1.18.2-1.19.2
	)
	swap("random_import",				 eval(mcVersion, ">=1.19.2") ? "net.minecraft.util.math.random.Random;" : "java.util.Random;")
	swap("translatable",				 eval(mcVersion, ">=1.19.2") ? "Text.translatable(key);" : "new TranslatableText(key);")
	swap("pack_name",					 eval(mcVersion, ">=1.19.3") ? "translated" : "String.valueOf(translated)")
	swap("world_view_arg",				 eval(mcVersion, ">=1.19.3") ? "WorldView" : "BlockView")
	swap("register",					 eval(mcVersion, ">=1.19.3") ? "Registry.register(Registries.SOUND_EVENT, id, SoundEvent.of(id));" : "Registry.register(Registry.SOUND_EVENT, id, new SoundEvent(id));")
	swap("modify_settings_import",		 eval(mcVersion, ">=1.20.1") ? "net.minecraft.block.MapColor;" : "net.minecraft.block.Material;")
	swap("modify_settings_target",	 	 eval(mcVersion, ">=1.20.1") ? "\"mapColor(Lnet/minecraft/block/MapColor;)\"" : "\"of(Lnet/minecraft/block/Material;)\"")
	swap("can_place_at_access_modifier", eval(mcVersion, ">=1.20.6") ? "protected" : "public")
	swap("identifier", 					 eval(mcVersion, ">=1.21"  ) ? "Identifier.of(MOD_ID, name);" : "new Identifier(MOD_ID, name);")
}

dependencies {
	minecraft "com.mojang:minecraft:${mcVersion}"
	mappings "net.fabricmc:yarn:${property("yarn_mappings")}:v2"
	modImplementation "net.fabricmc:fabric-loader:${property("loader_version")}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${property("fabric_version")}"
}

processResources {
	inputs.property("version", version)

	filesMatching("fabric.mod.json") {
		expand "version": version,
				"target": project.property("target_version")
	}
}

def javaVersion = stonecutter.eval(mcVersion, ">=1.20.6") ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17

tasks.withType(JavaCompile).configureEach {
	it.options.release = javaVersion.ordinal() + 1
}

if (stonecutter.current.isActive) { // run configs for non-active version would be invalid
	loom {
		runConfigs.all {
			ideConfigGenerated = true // generate IDE tasks for running client, server and testmod, datagen if those are present
			runDir "../../run" // use a global run directory for all versions
		}
	}
}

java {
	withSourcesJar()

	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}